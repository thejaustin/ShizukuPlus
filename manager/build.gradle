import java.nio.file.Paths
import org.gradle.process.ExecOperations
import com.android.build.gradle.internal.tasks.CompileArtProfileTask

plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dev.rikka.tools.refine"
    id "dev.rikka.tools.autoresconfig"
    id "dev.rikka.tools.materialthemebuilder"
}

android {
    namespace = "moe.shizuku.manager"
    ndkVersion "27.0.12077973"
    defaultConfig {
        applicationId = "moe.shizuku.privileged.api"
        versionCode = rootProject.ext.versionCode
        versionName = rootProject.ext.versionName
        externalNativeBuild {
            cmake {
                arguments = ["-DANDROID_STL=none"]
            }
        }
    }
    buildFeatures {
        buildConfig = true
        viewBinding = true
        prefab = true
    }
    signingConfigs {
        sign {}
    }
    buildTypes {
        debug {
            signingConfig = signingConfigs.sign
        }
        release {
            signingConfig = signingConfigs.sign
            minifyEnabled = true
            shrinkResources = true
            vcsInfo.include = false
            proguardFiles = ["proguard-rules.pro"]
        }
    }
    externalNativeBuild {
        cmake {
            path = file("src/main/jni/CMakeLists.txt")
            version = "3.31.0+"
        }
    }
    kotlinOptions {
        jvmTarget = "21"
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
        resources {
            excludes += ["**"]
        }
    }
    dependenciesInfo {
        includeInApk = false
    }
    lint {
        checkReleaseBuilds = false
    }
    sourceSets {
        main {
            aidl.srcDirs = ["src/main/aidl"]
        }
    }
}

autoResConfig {
    generatedClassFullName = "rikka.shizuku.manager.ShizukuLocales"
    generateRes = false
    generatedArrayFirstItem = "SYSTEM"
    generateLocaleConfig = true
}

materialThemeBuilder {
    themes {
        shizuku {
            primaryColor = "#3F51B5"
            lightThemeFormat = "Theme.Material3Expressive.Light.%s"
            lightThemeParent = "Theme.Material3Expressive.Light"
            darkThemeFormat = "Theme.Material3Expressive.Dark.%s"
            darkThemeParent = "Theme.Material3Expressive.Dark"
        }
        dynamic_colors_overlay {
            dynamicColors = true
            lightThemeFormat = "ThemeOverlay.Material3.DynamicColors.Light.Shizuku"
            lightThemeParent = "ThemeOverlay.Material3.DynamicColors.Light"
            darkThemeFormat = "ThemeOverlay.Material3.DynamicColors.Dark.Shizuku"
            darkThemeParent = "ThemeOverlay.Material3.DynamicColors.Dark"
        }
    }
    generatePalette = true
    generateTextColors = true
}

def collapseReleaseResourceNames = tasks.register("collapseReleaseResourceNames") {
    def aapt2 = Paths.get(project.android.sdkDirectory.path, "build-tools", project.android.buildToolsVersion, "aapt2")
    def zip = Paths.get(project.buildDir.path, "intermediates",
        "optimized_processed_res", "release", "optimizeReleaseResources", "resources-release-optimize.ap_")
    doLast {
        def optimized = new File("${zip}.opt")
        def execOps = services.get(ExecOperations)
        execOps.exec {
            commandLine aapt2.toFile(), "optimize", "--collapse-resource-names",
                "--resources-config-path", "aapt2-resources.cfg",
                "-o", optimized, zip.toFile()
            ignoreExitValue = false
        }
        if (optimized.exists()) {
            delete(zip.toFile())
            optimized.renameTo(zip.toFile())
        }
    }
}

afterEvaluate {
    tasks.getByName("optimizeReleaseResources").finalizedBy(collapseReleaseResourceNames)
    tasks.getByName("preReleaseBuild").dependsOn(":shell:assembleRelease")
    tasks.getByName("preDebugBuild").dependsOn(":shell:assembleDebug")
}

android.applicationVariants.configureEach { variant ->
    variant.outputs.configureEach {
        outputFileName = "shizuku-v${variant.versionName}-${variant.name}.apk"

        variant.assembleProvider.get().doLast {
            def outDir = new File(rootDir, "out")
            def mappingDir = new File(outDir, "mapping").absolutePath
            def apkDir = new File(outDir, "apk").absolutePath

            if (variant.getBuildType().isMinifyEnabled()) {
                copy {
                    from variant.mappingFileProvider.get()
                    into mappingDir
                    rename { String fileName -> "mapping-${variant.versionName}.txt" }
                }
                copy {
                    from outputFile
                    into apkDir
                }
            }
        }
    }
}

tasks.withType(CompileArtProfileTask.class).configureEach {
    enabled = false
}
configurations.configureEach {
    exclude group: "androidx.appcompat", module: "appcompat"
    exclude group: "androidx.profileinstaller", module: "profileinstaller"
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2"

    implementation project(":server")
    implementation project(":rish")
    implementation project(":starter")
    implementation project(":api")
    implementation project(":provider")

    implementation libs.hidden.compat
    compileOnly libs.hidden.stub

    implementation "androidx.browser:browser:1.8.0"
    implementation "androidx.core:core-ktx:1.16.0"
    implementation "androidx.fragment:fragment-ktx:1.8.7"
    implementation "androidx.recyclerview:recyclerview:1.4.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.9.0"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.work:work-runtime-ktx:2.10.4"

    implementation "com.google.android.material:material:1.14.0-alpha08"

    implementation "com.github.topjohnwu.libsu:core:6.0.0"

    implementation "dev.rikka.rikkax.appcompat:appcompat:1.6.1"
    implementation "dev.rikka.rikkax.compatibility:compatibility:2.0.0"
    implementation "dev.rikka.rikkax.core:core-ktx:1.4.1"
    implementation "dev.rikka.rikkax.material:material:2.7.2"
    implementation "dev.rikka.rikkax.material:material-preference:2.0.0"
    implementation "dev.rikka.rikkax.html:html-ktx:1.1.2"
    implementation "dev.rikka.rikkax.recyclerview:recyclerview-adapter:1.3.0"
    implementation "dev.rikka.rikkax.recyclerview:recyclerview-ktx:1.3.2"
    implementation "dev.rikka.rikkax.insets:insets:1.3.0"
    implementation "dev.rikka.rikkax.layoutinflater:layoutinflater:1.3.0"
    implementation "dev.rikka.rikkax.preference:simplemenu-preference:1.0.3"
    implementation "dev.rikka.rikkax.lifecycle:lifecycle-resource-livedata:1.0.1"

    implementation "io.github.vvb2060.ndk:boringssl:20250114"

    implementation "org.lsposed.libcxx:libcxx:27.0.12077973"
    implementation "org.lsposed.hiddenapibypass:hiddenapibypass:6.1"

    implementation "org.bouncycastle:bcpkix-jdk18on:1.80"

    implementation "me.zhanghai.android.appiconloader:appiconloader:1.5.0"
}

apply from: rootProject.file("signing.gradle")
